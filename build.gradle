group 'org.lightningj'

version '0.14.3-Beta'

apply plugin: "groovy"
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'project-report'
apply plugin: 'org.ajoberstar.git-publish'
apply plugin: 'signing'
apply plugin: 'maven'

description = "LightningJ - Lightning APIs for Java"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    // Spock releases are available from Maven Central
    mavenCentral()
    // Spock snapshots are available from the Sonatype OSS snapshot repository
   // maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://dl.bintray.com/asciidoctor/maven/"}
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.10"

        classpath "org.spockframework:spock-core:1.3-groovy-2.5"

        // optional dependencies for using Spock
        classpath "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
        classpath "net.bytebuddy:byte-buddy:1.9.7"          // allows mocking of classes (in addition to interfaces)
        classpath "org.objenesis:objenesis:2.5.1"    // allows mocking of classes without default constructor (together with CGLIB)

        // Documentation generation
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.7'
        classpath 'org.ajoberstar:gradle-git-publish:2.1.3'

        // JAX-B dependencies for JDK 9+
        classpath "javax.xml.bind:jaxb-api:2.2.11"
        classpath "com.sun.xml.bind:jaxb-core:2.2.11"
        classpath "com.sun.xml.bind:jaxb-impl:2.2.11"
        classpath "javax.activation:activation:1.1.1"
    }
}

dependencies {
    //gRPC
    compile 'com.google.api.grpc:googleapis-common-protos:0.0.3'
    compile 'io.netty:netty-tcnative-boringssl-static:2.0.40.Final'
    compile 'io.grpc:grpc-netty-shaded:1.39.0'
    compile 'io.grpc:grpc-protobuf:1.39.0'
    compile 'io.grpc:grpc-stub:1.39.0'

    // JSON-P
    compile 'javax.json:javax.json-api:1.0'
    compile 'org.glassfish:javax.json:1.1.2'

    // JAX-B dependencies for JDK 9+
    compile "javax.xml.bind:jaxb-api:2.2.11"
    compile "com.sun.xml.bind:jaxb-core:2.2.11"
    compile "com.sun.xml.bind:jaxb-impl:2.2.11"
    compile "javax.activation:activation:1.1.1"

    compile("javax.annotation:javax.annotation-api:1.3.2")

    // JMacaroon
   // compile 'com.github.nitram509:jmacaroons:0.3.1'

    // mandatory dependencies for using Spock
    testCompile "org.spockframework:spock-core:1.3-groovy-2.5"

    // optional dependencies for using Spock
    testCompile "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
    testRuntime "net.bytebuddy:byte-buddy:1.9.7"          // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:2.5.1"    // allows mocking of classes without default constructor (together with CGLIB)

}

/*
  Task for generating Lowlevel LND GRPC API
 */
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.7.1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.23.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

/**
 * Adds generated High Level APIs to the list of source directories to compile.
 */
compileJava {
    source += ['build/generated/source/wrapper/main/java']
}


/*
   Generate High Level LDN API using custom WrapperClassGenerator in buildSrc
 */
task generateWrappers(type: WrapperClassGenerator, dependsOn:compileJava){
    // When adding new protocols, remember to also add in WrapperFactory.
    protocols = ["lnrpc","autopilot","chainnotifier","invoices","router","signer","walletkit","watchtower","wtclient","verrpc", "walletunlocker","stateservice"]
}

/*
  Compile High Level LND API Java classes.
 */
task compileWrapperMessages(type: JavaCompile, dependsOn: generateWrappers){
    source = compileJava.source
    //classpath = files('build/classes/main')
    classpath = compileJava.classpath + files('build/classes/java/main')
    destinationDir = compileJava.destinationDir
}

/*
  Generate XSD of all generated High Level Message classes, fist dependency configuration then
  the actual task.
 */
configurations{
    xsdClassPathConf
}
dependencies {
    xsdClassPathConf 'javax.json:javax.json-api:1.0'
    xsdClassPathConf 'org.glassfish:javax.json:1.1.2'
}

task generateXSD(type: XSDGenerator, dependsOn: compileWrapperMessages){
    classpath=compileJava.classpath.asPath
    protocols = ["lnrpc","autopilot","chainnotifier","invoices","router","signer","walletkit","watchtower","wtclient","verrpc","walletunlocker","stateservice"]
}

/*
  Indicates that all high level api and XSD generation should be done before 'jar' task is executed.
 */
jar.dependsOn.add(generateXSD)

/*
  Adds generated source directories to be included in javadoc generation.
 */
javadoc {
    source += ['build/generated/source/proto/main/grpc','build/generated/source/proto/main/java','build/generated/source/wrapper/main/java']
    failOnError= false
    options.outputLevel = JavadocOutputLevel.QUIET
    options.addBooleanOption('Xdoclint:none', true)
}

asciidoctor {
    sourceDir = file('docs')
    outputDir = file('build/docs')
    attributes= [stylesheet: "$projectDir/docs/stylesheets/colony.css",
                 toc: 'left', 'toc-title': 'Table of Contents'
    ]
}

task ('doc',type: Copy,dependsOn: [javadoc, asciidoctor, htmlDependencyReport]){
    from('build/reports/project/'){
        include('dependencies/**')
    }
    from('build/docs/'){
        include('javadoc/**')
    }
    from('build/reports/tests'){
        include('test/**')
    }
    from('build/resources/main'){
        include('*.xsd')
    }
    from('LICENSE.txt'){
    }
    from('docs'){
        include('CNAME')
    }
    from('docs'){
        include('lightningj-release-pubkey.asc')
    }
    into('build/docs/html5')
}

/*
  Task for specifying for Intellij which source directories to use.
 */
idea {
    module {
        sourceDirs += new File('build/generated/source/proto/main/grpc')
        sourceDirs += new File('build/generated/source/proto/main/java')
        sourceDirs += new File('build/generated/source/wrapper/main/java')
        sourceDirs += new File('build/resources/main')
        sourceDirs += new File('src/examples')

        contentRoot = project.projectDir
    }
}

gitPublish {
    // where to publish to (repo must exist)
    repoUri = 'git@github.com:lightningj-org/lightningj.git'
    // (or 'https://github.com/ajoberstar/test-repo.git', depending on authentication)

    // branch will be created if it doesn't exist
    branch = 'gh-pages'

    // generally, you don't need to touch this
    //repoDir = file("$buildDir/somewhereelse") // defaults to $buildDir/gitPublish

    // what to publish, this is a standard CopySpec
    contents {
        from 'build/docs/html5'
    }

    // what to keep in the existing branch (include=keep)
//    preserve {
//        include '1.0.0/**'
//        exclude '1.0.0/temp.txt'
//    }

    // message used when committing changes
    commitMessage = 'Publishing a new page' // defaults to 'Generated by gradle-git-publish'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    from 'build/generated/source/proto/main/grpc'
    from 'build/generated/source/proto/main/java'
    from 'build/generated/source/wrapper/main/java'
}

artifacts {
    archives javadocJar, sourcesJar
}

// Add integration test task, with source sets etc.
apply from: 'integrationTest.gradle'

// Use external gpg2 command to support signing by hardware keys (i.e. SmartCard) and sign only
// if build should be uploaded to Maven Central.
signing {
    required { gradle.taskGraph.hasTask(uploadArchives) }
    useGpgCmd()
    sign configurations.archives
}

signArchives.onlyIf {gradle.taskGraph.hasTask(uploadArchives)}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'lightningj'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'LightningJ - Lightning APIs for Java'
                url 'http://www.lightningj.org'

                scm {
                    connection 'https://github.com/lightningj-org/lightningj.git'
                    developerConnection 'https://github.com/lightningj-org/lightningj.git'
                    url 'https://github.com/lightningj-org/lightningj'
                }

                licenses {
                    license {
                        name 'GNU Lesser General Public License v3.0'
                        url 'http://www.gnu.org/licenses/lgpl.txt'
                    }
                }

                developers {
                    developer {
                        id 'herrvendil'
                        name 'Philip Vendil'
                        email 'info@lightningj.org'
                    }
                }
            }
        }
    }
}

uploadArchives.dependsOn.add(signArchives)

